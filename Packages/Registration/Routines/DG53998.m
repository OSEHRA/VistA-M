DG53998 ;WILM/BDB - DG*5.3*998 PRE INSTALL INELIGIBLE, HUMANITARIAN AND VHAP UPDATES;03/02/19 9:18pm
 ;;5.3;Registration;**998**;Aug 13, 1993;Build 15
 ;;Per VHA Directive 2004-038, this routine should not be modified.
 ;
 ;  Integration Agreements:
 ;        10141 : BMES^XPDUTL
 ;              : MES^XPDUTL
 ;        10104 : UP^XLFSTR
 ;
 Q
 ; This pre-install routine will be used to perform the following task:
 ; Update the NAME (#.01) field of the HEALTH BENEFIT PLAN (#25.11) file.
 ;
PRE ; Entry point for pre-install
 ;
 L +^DGHBP(25.11,0):10 I '$T D BMES^XPDUTL("Health Benefit Plan (#25.11) File is locked by another user.  Please log YOUR IT Services ticket.") Q
 ;
 ; Update NAME (#.01) field of the HEALTH BENEFIT PLAN (#25.11) file.
 D BMES^XPDUTL("Updating the NAME (#.01) field of the Health Benefit Plan (#25.11) File ")
 D UPDATE
 D BMES^XPDUTL("Update of the NAME field of the Health Benefit Plans has completed. ")
 ;
 L -^DGHBP(25.11,0)
 Q
 ;
UPDATE ;Change Name field of certain Health Benefit Plans
 ;
 N DGN,DGTEXT,DGOLD,DGNEW,DGIEN,DGFND,DGHBPN
 F DGN=1:1 S DGTEXT=$P($T(NAMES+DGN),";;",2) Q:DGTEXT=""  D
 .S DGOLD=$P(DGTEXT,";",1),DGNEW=$P(DGTEXT,";",2)
 .S DGOLD=$$UP^XLFSTR(DGOLD),DGNEW=$$UP^XLFSTR(DGNEW)
 .S DGIEN=0,DGFND=0 F  S DGIEN=$O(^DGHBP(25.11,DGIEN)) Q:+DGIEN=0  D  Q:DGFND=1
 ..S DGHBPN=$P(^DGHBP(25.11,DGIEN,0),U,1) Q:DGHBPN'=DGOLD
 ..S DGFND=1
 ..S DR=".01///"_DGNEW,DIE="^DGHBP(25.11,",DA=DGIEN D ^DIE K DIE,DR,DA
 ..D BMES^XPDUTL("PLAN NAME:")
 ..D MES^XPDUTL(DGOLD)
 ..D MES^XPDUTL("RENAMED TO:")
 ..D MES^XPDUTL(DGNEW)
 Q
DGERR ;
 D BMES^XPDUTL("    *** An Error occurred during the updating of ")
 D MES^XPDUTL("    the NAME field for the Health Benefit Plans for this Plan:")
 D MES^XPDUTL("    "_DGOLD)
 D MES^XPDUTL("    The Plan was not located on file.")
 D MES^XPDUTL("    Please log YOUR IT Services ticket. ***")
 Q
NAMES ;format:  ;;oldname;newname
 ;;VETERAN PLAN - CCP BASIC;VETERAN PLAN CCP BASIC
 ;;VETERAN PLAN - CCP STATE WITH NO FULL-SERVICE MEDICAL FACILITY;VETERAN PLAN CCP STATE WITH NO FULL SERVICE MEDICAL FACILITY
 ;;VETERAN PLAN - CCP URGENT CARE;VETERAN PLAN CCP URGENT CARE
 ;;VETERAN PLAN - CCP GRANDFATHER;VETERAN PLAN CCP GRANDFATHER
 ;;VETERAN PLAN - CCP HARDSHIP DETERMINATION;VETERAN PLAN CCP HARDSHIP DETERMINATION
 ;;COMPREHENSIVE EXTENDED CARE SERVICES COPAY EXEMPT;VA MANDATORY NURSING HOME ELIGIBLE COPAY EXEMPT
 ;;VETERAN EXTENDED CARE SERVICES COPAY EXEMPT;VA EXTENDED CARE SERVICES COPAY EXEMPT
 ;;VETERAN EXTENDED CARE SERVICES COPAY REQ;VA EXTENDED CARE SERVICES COPAY REQUIRED
 ;;EXTENDED CARE SERVICES HUMANITARIAN;VA EXTENDED CARE SERVICES INELIGIBLE
 ;;
